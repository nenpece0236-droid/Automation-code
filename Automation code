// Uncomment the following line to enable serial debug output
//#define ENABLE_DEBUG
#ifdef ENABLE_DEBUG
  #define DEBUG_ESP_PORT Serial
  #define NODEBUG_WEBSOCKETS
  #define NDEBUG
#endif 
#include <Arduino.h>
#include <ESP8266WiFi.h>
#include "SinricPro.h"
#include "SinricProSwitch.h"
#include <map>
// =============================
// WiFi & SinricPro Credentials
// =============================
#define WIFI_SSID     "VFSTR-WIFI"    
#define WIFI_PASS     ""
#define APP_KEY       "3348db8a-6e27-4d5b-9e2d-f0f3a4c0fd99"
#define APP_SECRET    "8116f574-e6e9-48f9-9aac-2bc7accd95c5-60fce3d1-412f-4de9-935d-0ef4a04b96c7"
// =============================
// SinricPro Device IDs (Only 3)
// =============================
#define device_ID_1   "68e92693359ccc32ce0e4d5e"
#define device_ID_2   "68e9258a5918d860c0a2bb68"
#define device_ID_3   "68e925f1359ccc32ce0e4d18"
// =============================
// GPIO Pin Configuration
// =============================
#define RelayPin1   5   // D1
#define RelayPin2   4   // D2
#define RelayPin3   14  // D5
#define SwitchPin1  10  // SD3
#define SwitchPin2  0   // D3
#define SwitchPin3  13  // D7
#define wifiLed     16  // D0
// Uncomment this if using tactile push buttons (momentary)
//#define TACTILE_BUTTON 1
#define BAUD_RATE       9600
#define DEBOUNCE_TIME   250
// =============================
// Struct Definitions
// =============================
typedef struct {
  int relayPIN;
  int flipSwitchPIN;
} deviceConfig_t;
typedef struct {
  String deviceId;
  bool lastFlipSwitchState;
  unsigned long lastFlipSwitchChange;
} flipSwitchConfig_t;
// =============================
// Device Configurations
// =============================
std::map<String, deviceConfig_t> devices = {
  {device_ID_1, {RelayPin1, SwitchPin1}},
  {device_ID_2, {RelayPin2, SwitchPin2}},
  {device_ID_3, {RelayPin3, SwitchPin3}}
};
std::map<int, flipSwitchConfig_t> flipSwitches;
// =============================
// Function Definitions
// =============================
void setupRelays() { 
  for (auto &device : devices) {
    int relayPIN = device.second.relayPIN;
    pinMode(relayPIN, OUTPUT);
    digitalWrite(relayPIN, HIGH);  // default OFF
  }
}
void setupFlipSwitches() {
  for (auto &device : devices) {
    flipSwitchConfig_t flipSwitchConfig;
    flipSwitchConfig.deviceId = device.first;
    flipSwitchConfig.lastFlipSwitchChange = 0;
    flipSwitchConfig.lastFlipSwitchState = true;
    int flipSwitchPIN = device.second.flipSwitchPIN;
    flipSwitches[flipSwitchPIN] = flipSwitchConfig;
    pinMode(flipSwitchPIN, INPUT_PULLUP);
  }
}
bool onPowerState(String deviceId, bool &state) {
  Serial.printf("%s: %s\r\n", deviceId.c_str(), state ? "on" : "off");
  int relayPIN = devices[deviceId].relayPIN;
  digitalWrite(relayPIN, !state);
  return true;
}
void handleFlipSwitches() {
  unsigned long actualMillis = millis();
  for (auto &flipSwitch : flipSwitches) {
    unsigned long lastChange = flipSwitch.second.lastFlipSwitchChange;
    if (actualMillis - lastChange > DEBOUNCE_TIME) {
      int flipSwitchPIN = flipSwitch.first;
      bool lastState = flipSwitch.second.lastFlipSwitchState;
      bool currentState = digitalRead(flipSwitchPIN);
      if (currentState != lastState) {
#ifdef TACTILE_BUTTON
        if (currentState) {
#endif
          flipSwitch.second.lastFlipSwitchChange = actualMillis;
          String deviceId = flipSwitch.second.deviceId;
          int relayPIN = devices[deviceId].relayPIN;
          bool newRelayState = !digitalRead(relayPIN);
          digitalWrite(relayPIN, newRelayState);
          SinricProSwitch &mySwitch = SinricPro[deviceId];
          mySwitch.sendPowerStateEvent(!newRelayState);
#ifdef TACTILE_BUTTON
        }
#endif
        flipSwitch.second.lastFlipSwitchState = currentState;
      }
    }
  }
}
void setupWiFi() {
  Serial.printf("\r\n[WiFi]: Connecting");
  WiFi.begin(WIFI_SSID, WIFI_PASS);
  while (WiFi.status() != WL_CONNECTED) {
    Serial.printf(".");
    delay(250);
  }
  digitalWrite(wifiLed, LOW);
  Serial.printf("connected!\r\n[WiFi]: IP Address: %s\r\n", WiFi.localIP().toString().c_str());
}
void setupSinricPro() {
  for (auto &device : devices) {
    const char *deviceId = device.first.c_str();
    SinricProSwitch &mySwitch = SinricPro[deviceId];
    mySwitch.onPowerState(onPowerState);
  }
  SinricPro.begin(APP_KEY, APP_SECRET);
  SinricPro.restoreDeviceStates(true);
}
// =============================
// Arduino Core Functions
// =============================
void setup() {
  Serial.begin(BAUD_RATE);
  pinMode(wifiLed, OUTPUT);
  digitalWrite(wifiLed, HIGH);
  setupRelays();
  setupFlipSwitches();
  setupWiFi();
  setupSinricPro();
}
void loop() {
  SinricPro.handle();
  handleFlipSwitches();
}
